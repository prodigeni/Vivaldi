<toplevel> ::= <expression>
             | <expression> <division> <toplevel>

<expression> ::= <prec12>

<nonop_expression> ::= <array_literal>
                       | <assignment>
                       | <block>
                       | <cond_statement>
                       | <except>
                       | <for_loop>
                       | <function_definition>
                       | <literal>
                       | <new_obj>
                       | <require>
                       | <return>
                       | <try_catch>
                       | <type_definition>
                       | <variable_declaration>
                       | <variable>
                       | <while_loop>

<prec0> ::= <nonop_expression>
          | <prec0> '.' <variable>
          | <prec0> '(' <expr_list> ')'

<prec1> ::= <prec0>
          | '!' <prec1>
          | '~' <prec1>
          | '-' <prec1>

<prec2> ::= <prec1>
          | <prec1> '**' <prec2>

<prec3> ::= <prec2>
          | <prec2> '*' <prec3>
          | <prec2> '/' <prec3>
          | <prec2> '%' <prec3>

<prec4> ::= <prec3>
          | <prec3> '+' <prec4>
          | <prec3> '-' <prec4>

<prec5> ::= <prec4>
          | <prec4> '>>' <prec5>
          | <prec4> '<<' <prec5>

<prec6> ::= <prec5>
          | <prec5> '&' <prec6>

<prec7> ::= <prec6>
          | <prec6> '^' <prec7>

<prec8> ::= <prec7>
          | <prec7> '|' <prec8>

<prec9> ::= <prec8>
          | <prec8> 'to' <prec9>

<prec10> ::= <prec9>
           | <prec9> '>' <prec10>
           | <prec9> '<' <prec10>
           | <prec9> '>=' <prec10>
           | <prec9> '<=' <prec10>

<prec11> ::= <prec10>
           | <prec10> '==' <prec11>
           | <prec10> '!=' <prec11>

<prec12> ::= <prec11>
           | <prec11> '&&' <prec12>

<prec13> ::= <prec12>
           | <prec12> '||' <prec13>

<array_literal> ::= '[' <expr_list> ']'

<assignment> ::= <variable> '=' <expression>

<block> ::= 'do' <toplevel> 'end'

<cond_statement> ::= 'cond' <inner_cond_statement>
                   | 'if' <inner_cond_statement>

<except> ::= 'except' <expression>

<for_loop> ::= 'for' <variable> 'in' <expression> ':' <expression>

<function_definition> ::= 'fn' '(' <arg_list> ')' ':' <expression>
                        | 'fn' <variable> '(' <arg_list> ')' ':' <expression>

<literal> ::= <bool>
            | <float>
            | <integer>
            | <nil>
            | <string>
            | <symbol>

<new_obj> ::= 'new' <variable> '(' <expr_list> ')'

<require> ::= 'require' <string>

<return> ::= 'return' <expression>

<try_catch> ::= 'try' ':' <expression> <division 'catch' <variable> ':' <expression>

<type_definition> ::= 'variable' <name> <method_list>

<variable_declaration> ::= 'let' <variable> '=' <expression>

<variable> ::= /\([^[:space:][:punct:]\|_\)+/

<while_loop> ::= 'while' <expression> ':' <expression>

<symbol> ::= '\'' <variable>

<integer> ::= /[1-9][0-9]* /
            | /0[0-7]* /
            | /0x[0-9a-fA-F]+/
            | /0b[01]+/

<float> ::= /[1-9][0-9]*\.[0-9]+/
          | /0\.[0-9]+/

<bool> ::= 'true'
         | 'false'

<nil> ::= 'nil'

<string> ::= /".*"/

<division> ::= '\n'
             | ';'
